plugins {
    id 'com.github.spotbugs' version '4.7.8' apply false
    id 'eclipse'
    id 'idea'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE' apply false
    id 'org.sonarqube' version '3.3' apply false
}

allprojects {
    apply plugin: 'checkstyle'
    apply plugin: 'com.github.spotbugs'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'org.sonarqube'

    group = 'co.previewme'

    ext.major = 0
    ext.minor = 1
    ext.ci = System.getenv('CI')
    ext.run = System.getenv('GITHUB_RUN_NUMBER') ?: '0'
    version = ext.major + '.' + ext.minor + '.' + ext.run;
    if (ext.ci != 'true') {
        version = version + '-SNAPSHOT'
    }

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    compileJava.options.encoding = "UTF-8"
    compileTestJava.options.encoding = "UTF-8"

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        mavenCentral()
    }

    ext.springCloudVersion = '2020.0.4'

    test {
        finalizedBy jacocoTestReport
        useJUnitPlatform()
    }

    tasks.withType(Checkstyle) {
        reports {
            xml.required = false
            html.required = true
        }
    }

    jacocoTestReport {
        dependsOn test
        reports {
            xml.required = true
            csv.required = false
            html.required = true
        }
    }

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
        testImplementation 'org.assertj:assertj-core:3.21.0'
        testImplementation 'org.mockito:mockito-core:4.0.0'
        testImplementation 'org.mockito:mockito-junit-jupiter:4.0.0'
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${ext.springCloudVersion}"
        }
    }
}

sonarqube {
    properties {
        property 'sonar.projectKey', "previewme_${rootProject.name}"
        property 'sonar.host.url', 'https://sonarcloud.io'
        property 'sonar.organization', 'previewme'
    }
}